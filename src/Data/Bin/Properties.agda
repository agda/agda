------------------------------------------------------------------------
-- The Agda standard library
--
-- Properties of the binary representation of natural numbers
------------------------------------------------------------------------

module Data.Bin.Properties where

open import Data.Bin
open import Data.Digit using (Bit)
import Data.Fin as Fin
import Data.Fin.Properties as ğ”½â‚š
open import Data.List.Base using (List; []; _âˆ·_)
open import Data.List.Properties using (âˆ·-injective)
open import Data.Nat
  using (â„•; zero; zâ‰¤n; sâ‰¤s; â‰¤-pred)
  renaming (suc to 1+_; _+_ to _+â„•_; _*_ to _*â„•_)
import Data.Nat.Properties as â„•â‚š
open import Data.Product using (projâ‚; projâ‚‚)
open import Function using (_âˆ˜_)
open import Relation.Binary
open import Relation.Binary.Consequences
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; _â‰¢_; refl; sym; isEquivalence; respâ‚‚; decSetoid)
open import Relation.Nullary using (yes; no)

------------------------------------------------------------------------
-- (Bin, _â‰¡_) is a decidable setoid

1#-injective : âˆ€ {as bs} â†’ as 1# â‰¡ bs 1# â†’ as â‰¡ bs
1#-injective refl = refl

infix 4 _â‰Ÿ_ _â‰ŸLB_

_â‰ŸLB_ : Decidable (_â‰¡_ {A = List Bit})
_â‰ŸLB_ []       []       = yes refl
_â‰ŸLB_ []       (_ âˆ· _)  = no Î»()
_â‰ŸLB_ (_ âˆ· _) []        = no Î»()
_â‰ŸLB_ (x âˆ· xs) (y âˆ· ys) with x ğ”½â‚š.â‰Ÿ y | xs â‰ŸLB ys
... | _        | no xsâ‰¢ys = no (xsâ‰¢ys âˆ˜ projâ‚‚ âˆ˜ âˆ·-injective)
... | no  xâ‰¢y  | _        = no (xâ‰¢y   âˆ˜ projâ‚ âˆ˜ âˆ·-injective)
... | yes refl | yes refl = yes refl

_â‰Ÿ_ : Decidable {A = Bin} _â‰¡_
0#    â‰Ÿ 0#    = yes refl
0#    â‰Ÿ bs 1# = no Î»()
as 1# â‰Ÿ 0#    = no Î»()
as 1# â‰Ÿ bs 1# with as â‰ŸLB bs
... | yes refl  = yes refl
... | no  asâ‰¢bs = no (asâ‰¢bs âˆ˜ 1#-injective)

â‰¡-isDecEquivalence : IsDecEquivalence _â‰¡_
â‰¡-isDecEquivalence = record
  { isEquivalence = isEquivalence
  ; _â‰Ÿ_           = _â‰Ÿ_
  }

â‰¡-decSetoid : DecSetoid _ _
â‰¡-decSetoid = decSetoid _â‰Ÿ_

------------------------------------------------------------------------
-- (Bin _â‰¡_ _<_) is a strict total order

<-trans : Transitive _<_
<-trans (less ltâ‚) (less ltâ‚‚) = less (â„•â‚š.<-trans ltâ‚ ltâ‚‚)

<-asym : Asymmetric _<_
<-asym (less lt) (less gt) = â„•â‚š.<-asym lt gt

<-irrefl : Irreflexive _â‰¡_ _<_
<-irrefl refl (less lt) = â„•â‚š.<-irrefl refl lt

âˆ·Ê³-mono-< : âˆ€ {a b as bs} â†’ as 1# < bs 1# â†’ (a âˆ· as) 1# < (b âˆ· bs) 1#
âˆ·Ê³-mono-< {a} {b} {as} {bs} (less lt) = less (begin
  1+ (mâ‚ +â„• nâ‚ *â„• 2) â‰¤âŸ¨ sâ‰¤s (â„•â‚š.+-mono-â‰¤ (â‰¤-pred (ğ”½â‚š.bounded a)) â„•â‚š.â‰¤-refl) âŸ©
  1+ (1 +â„• nâ‚ *â„• 2)  â‰¡âŸ¨ refl âŸ©
  1+ nâ‚ *â„• 2         â‰¤âŸ¨ â„•â‚š.*-mono-â‰¤ lt â„•â‚š.â‰¤-refl âŸ©
  nâ‚‚ *â„• 2            â‰¤âŸ¨ â„•â‚š.nâ‰¤m+n mâ‚‚ (nâ‚‚ *â„• 2) âŸ©
  mâ‚‚ +â„• nâ‚‚ *â„• 2      âˆ)
  where
  open â„•â‚š.â‰¤-Reasoning
  mâ‚ = Fin.toâ„• a;   mâ‚‚ = Fin.toâ„• b
  nâ‚ = toâ„• (as 1#); nâ‚‚ = toâ„• (bs 1#)

âˆ·Ë¡-mono-< : âˆ€ {a b bs} â†’ a Fin.< b â†’ (a âˆ· bs) 1# < (b âˆ· bs) 1#
âˆ·Ë¡-mono-< {a} {b} {bs} lt = less (begin
  1 +â„• (mâ‚  +â„• n *â„• 2)  â‰¡âŸ¨ sym (â„•â‚š.+-assoc 1 mâ‚ (n *â„• 2)) âŸ©
  (1 +â„• mâ‚) +â„• n *â„• 2   â‰¤âŸ¨ â„•â‚š.+-mono-â‰¤ lt â„•â‚š.â‰¤-refl âŸ©
  mâ‚‚  +â„• n *â„• 2   âˆ)
  where
  open â„•â‚š.â‰¤-Reasoning
  mâ‚ = Fin.toâ„• a; mâ‚‚ = Fin.toâ„• b; n = toâ„• (bs 1#)

1<[23] : âˆ€ {b} â†’ [] 1# < (b âˆ· []) 1#
1<[23] {b} = less (â„•â‚š.nâ‰¤m+n (Fin.toâ„• b) 2)

1<2+ : âˆ€ {b bs} â†’ [] 1# < (b âˆ· bs) 1#
1<2+ {_} {[]}     = 1<[23]
1<2+ {_} {b âˆ· bs} = <-trans 1<[23] (âˆ·Ê³-mono-< {a = b} 1<2+)

0<1+ : âˆ€ {bs} â†’ 0# < bs 1#
0<1+ {[]}     = less (sâ‰¤s zâ‰¤n)
0<1+ {b âˆ· bs} = <-trans (less (sâ‰¤s zâ‰¤n)) 1<2+

<â‡’â‰¢ : âˆ€ {a b} â†’ a < b â†’ a â‰¢ b
<â‡’â‰¢ lt eq = asymâŸ¶irr (respâ‚‚ _<_) sym <-asym eq lt

<-cmp : Trichotomous _â‰¡_ _<_
<-cmp 0#            0#            = triâ‰ˆ (<-irrefl refl) refl (<-irrefl refl)
<-cmp 0#            (_ 1#)        = tri< 0<1+ (<â‡’â‰¢ 0<1+) (<-asym 0<1+)
<-cmp (_ 1#)        0#            = tri> (<-asym 0<1+) (<â‡’â‰¢ 0<1+ âˆ˜ sym) 0<1+
<-cmp ([] 1#)       ([] 1#)       = triâ‰ˆ (<-irrefl refl) refl (<-irrefl refl)
<-cmp ([] 1#)       ((b âˆ· bs) 1#) = tri< 1<2+ (<â‡’â‰¢ 1<2+) (<-asym 1<2+)
<-cmp ((a âˆ· as) 1#) ([] 1#)       = tri> (<-asym 1<2+) (<â‡’â‰¢ 1<2+ âˆ˜ sym) 1<2+
<-cmp ((a âˆ· as) 1#) ((b âˆ· bs) 1#) with <-cmp (as 1#) (bs 1#)
... | tri<  lt Â¬eq Â¬gt =
  tri< (âˆ·Ê³-mono-< lt)  (<â‡’â‰¢ (âˆ·Ê³-mono-< lt)) (<-asym (âˆ·Ê³-mono-< lt))
... | tri> Â¬lt Â¬eq  gt =
  tri> (<-asym (âˆ·Ê³-mono-< gt)) (<â‡’â‰¢ (âˆ·Ê³-mono-< gt) âˆ˜ sym) (âˆ·Ê³-mono-< gt)
... | triâ‰ˆ Â¬lt refl Â¬gt with ğ”½â‚š.cmp a b
...   | triâ‰ˆ Â¬ltâ€² refl Â¬gtâ€² =
  triâ‰ˆ (<-irrefl refl) refl (<-irrefl refl)
...   | tri<  ltâ€² Â¬eq  Â¬gtâ€² =
  tri< (âˆ·Ë¡-mono-< ltâ€²)  (<â‡’â‰¢ (âˆ·Ë¡-mono-< ltâ€²)) (<-asym (âˆ·Ë¡-mono-< ltâ€²))
...   | tri> Â¬ltâ€² Â¬eq  gtâ€²  =
  tri> (<-asym (âˆ·Ë¡-mono-< gtâ€²)) (<â‡’â‰¢ (âˆ·Ë¡-mono-< gtâ€²) âˆ˜ sym) (âˆ·Ë¡-mono-< gtâ€²)

<-isStrictTotalOrder : IsStrictTotalOrder _â‰¡_ _<_
<-isStrictTotalOrder = record
  { isEquivalence = isEquivalence
  ; trans         = <-trans
  ; compare       = <-cmp
  }

<-strictTotalOrder : StrictTotalOrder _ _ _
<-strictTotalOrder = record
  { Carrier            = Bin
  ; _â‰ˆ_                = _â‰¡_
  ; _<_                = _<_
  ; isStrictTotalOrder = <-isStrictTotalOrder
  }
